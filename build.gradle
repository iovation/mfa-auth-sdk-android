// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.4.20'
    ext.dokka_version = '1.4.32'
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath 'com.google.gms:google-services:4.3.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "org.sonarqube" version "2.7"
    id 'base'
}

sonarqube {
    properties {
        def appConfig = project('app').android.defaultConfig
        property "sonar.projectName", "c3-android-auth-sdk"
        property "sonar.projectVersion", "${appConfig.versionName}-${appConfig.versionCode}"
        property "sonar.verbose", "true"
        property "sonar.sourceEncoding", "UTF-8"
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url "https://github.com/iovation/launchkey-android-authenticator-sdk/raw/master/lk-auth-sdk" }
        maven { url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven" }
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }
}

subprojects {
    sonarqube {
        properties {
            property "sonar.sources", "src"
            property "sonar.tests", ["src/test/java"]
            property "sonar.test.inclusions", "**/*Test*/**"
        }
    }
}
